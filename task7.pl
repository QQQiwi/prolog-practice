запомнить(Арг, Арг2, да):- assert(верно(Арг, Арг2)).
запомнить(Арг, Арг2, нет):- assert(ложно(Арг, Арг2)).

узнать(Арг, Арг2, Ответ):- write(Арг), write(" "), write(Арг2), read(Ответ), запомнить(Арг, Арг2, Ответ).

свойство(Арг, Арг2):- верно(Арг, Арг2), !.
свойство(Арг, Арг2):- not(ложно(Арг, Арг2)), узнать(Арг, Арг2, Ответ), Ответ=да.

нет_свойства(Арг, Арг2):- ложно(Арг, Арг2), !.
нет_свойства(Арг, Арг2):- not(верно(Арг, Арг2)), узнать(Арг, Арг2, Ответ), Ответ=нет.


это("алгоритм"):- свойство("Его суть", "основана на статистике"),
                  not(свойство("У него", "есть скрытые слои")).
это("нейросеть"):- свойство("У него", "есть скрытые слои"),
                   свойство("Его", "архитектуру можно настроить").
это("трансформер"):- это("нейросеть"), свойство("Ему", "нужно много данных").

это("задача Natural Language Processing"):- свойство("Данные это", "текст").
это("задача Computer Vision"):- свойство("Данные это", "изображения").
это("задача Machine Learning"):- свойство("Данные это", "табличные значения").
это("задача смежных областей"):- свойство("Данные это", "неоднородные объекты").

это("обучение с учителем"):- свойство("Для каждого объекта в данных",
                                      "есть эталонный пример ответа").
это("обучение без учителя"):- свойство("Для каждого объекта в данных",
                                       "нет эталонного примера ответа").

это("задача классификации"):- это("обучение с учителем"), свойство("Нужно определить", "класс объекта").
это("задача кластеризации"):- это("обучение без учителя"), свойство("Нужно определить", "класс объекта").
это("задача регрессии"):- это("обучение с учителем"), свойство("Нужно определить", "числовое значение").
это("задача генерации"):- это("обучение без учителя"), свойство("Нужно сгенерировать", "похожие данные").

это("определение объектов на изображении"):- это("обучение с учителем"),
                                             это("задача Computer Vision"),
                                             свойство("нужно", "определять объекты на изображении").

это("классификация изображений"):- это("задача классификации"),
                                   это("задача Computer Vision").

это("сегментация изображений"):- это("обучение с учителем"),
                                 это("задача Computer Vision"),
                                 свойство("нужно ", "делить изображения на сегменты").

это("генерация изображений"):- это("задача генерации"),
                               это("задача Computer Vision").

это("классификация текста"):- это("задача классификации"),
                              это("задача Natural Language Processing").

это("генерация текста"):- это("задача генерации"),
                          это("задача Natural Language Processing").

цель:- алгоритм(Арг), !, write("Попробуй алгоритм: "), writeln(Арг), очистить.
цель:- write("К сожалению, я не знаю какой алгоритм подсказать."), очистить.

алгоритм("Случайный лес"):- это("алгоритм"),
                            (это("задача классификации"); это("задача регрессии")),
                            это("задача Machine Learning"),
                            свойство("Он должен", "хорошо обобщаться").

алгоритм("Метод k ближайших соседей"):- это("алгоритм"),
                                        (это("задача кластеризации"); это("задача регрессии")),
                                        это("задача Machine Learning").

алгоритм("Метод опорных векторов"):- это("алгоритм"),
                                     (это("задача классификации"); это("задача регрессии")),
                                     это("задача Machine Learning"),
                                     свойство("Он", "основан на разделении пространства признаков").

алгоритм("DeiT или ViT"):- это("трансформер"),
                           это("классификация изображений").

алгоритм("GPT"):- это("трансформер"),
                  это("задача генерации"),
                  это("задача смежных областей").

алгоритм("ViLT"):- это("трансформер"),
                   это("задача классификации"),
                   это("задача смежных областей").

алгоритм("Bert"):- это("трансформер"),
                   это("классификация текста").

алгоритм("Resnet50 или Inception_v3"):- это("нейросеть"),
                                        это("классификация изображений").

алгоритм("RNN или LSTM"):- это("нейросеть"),
                           это("задача регрессии"),
                           это("задача Machine Learning").

:- dynamic верно/2, ложно/2.
очистить:- retract(верно(_,_)), fail.
очистить:- retract(ложно(_,_)), fail.
